Sub ShiftBid()
'--------------------------------------------------------------------------------------------------------------------
'Prereqs:
'You must redefine the ws name to the name of your ws with the two tables below.
'Requires two input tables:
'1. Table 1 named "Shifts" This table will have two columns "Shift" and "Capacity" where
'   a. Shifts lists the potential shifts offered (in the same format as the reps choices)
'   b. Capacity lists the maximum number of reps that can be in that shift
'2. Table 2 named "VBAChoicesPivoted" This table will have a variable number of columns but they will be structured:
'   a. First column is the rep name
'   b. Second column is the rep rank
'   c. Columns 3-x are the reps choices of shifts where the further left the column is the more prefered the shift is.
'--------------------------------------------------------------------------------------------------------------------
'Define Scope
Dim wb As Workbook: Set wb = ActiveWorkbook
Dim ws As Worksheet: Set ws = wb.Sheets("Data")
'Define Tables
Dim tblChoices As ListObject: Set tblChoices = ws.ListObjects("VBAChoicesPivoted")
Dim tblShifts As ListObject: Set tblShifts = ws.ListObjects("Shifts")
'Define Arrays
Dim arrChoice As Variant, arrShifts As Variant, arrResults As Variant
'Define Vars
Dim i As Integer, j As Integer, intChoice As Integer
Dim CurrentShift As String

'Sort Choices table
With ws.ListObjects("VBAChoicesPivoted").Sort
    .SortFields.Clear
    .SortFields.Add key:=Range("VBAChoicesPivoted[Rank]"), SortOn:=xlSortOnValues, Order:=xlAscending
    .Header = xlYes
    .Apply
End With

'Set Arrays
arrChoice = tblChoices.DataBodyRange
arrShifts = tblShifts.DataBodyRange


'Populate Results Table
ReDim arrResults(1 To UBound(arrChoice) + 1, 1 To 2)
arrResults(1, 1) = "Rep Name"
arrResults(1, 2) = "Shift Assigned"
'Loop Through Reps Choices
For i = 1 To UBound(arrChoice)
    'Define Results Array
    arrResults(i + 1, 1) = arrChoice(i, 1)
    'Loop through choices to find best choice
    For intChoice = 3 To 5 Step 1
    'Define Current Shift Choice
    CurrentShift = arrChoice(i, intChoice)
        'Loop through shifts and their capacity to find availability
        For j = 1 To UBound(arrShifts)
            'If the shift in the array is equal to the current choice then proceed
            If arrShifts(j, 1) = CurrentShift Then
                'If the shift has capacity then proceed
                If arrShifts(j, 2) > 0 Then
                    arrShifts(j, 2) = arrShifts(j, 2) - 1
                    arrResults(i + 1, 2) = CurrentShift
                    'If capacity is found don't continue the loop go to the next rep.
                    GoTo nextrep:
                End If
            End If
        Next j
    Next intChoice
nextrep:
Next i

'Print the array to a range
Application.InputBox("Select Range to place the results", "Select Range", Type:=8).Resize((UBound(arrResults, 1)), 2) = arrResults

End Sub
