Sub LastFirstMidToFirstMidLast()
Dim Rng As Range, Cell As Range
Dim Delim As String, LastName As String, FirstMidName As String

On Error GoTo Handler:

Delim = Application.InputBox("Enter the delimiter between the last name and first name", "Enter Delimiter", ",", Type:=2)
Set Rng = Application.InputBox("Select the range to modify", "Select Range", Selection.Address, Type:=8)

Call FasterMacros(True) ' Optional but recommended

For Each Cell In Rng
    LastName = Trim(Left(Cell.Value, InStr(Cell, Delim) - 1))
    FirstMidName = Trim(Right(Cell.Value, (Len(Cell.Value) - Len(LastName) - 1)))
    Cell.Value = FirstMidName & " " & LastName
Next Cell

Handler:
Call FasterMacros(False) ' Optional but recommended

End Sub

Sub FirstMidLastToLastFirstMid()
Dim Rng As Range, Cell As Range
Dim LastName As String, FirstMidName As String

On Error GoTo Handler:

Set Rng = Application.InputBox("Select the range to modify", "Select Range", Selection.Address, Type:=8)

Call FasterMacros(True) ' Optional but recommended

For Each Cell In Rng
    LastName = Trim(Right(Cell.Value, (Len(Cell.Value) - InStrRev(Cell.Value, " "))))
    FirstMidName = Trim(Left(Cell.Value, (InStrRev(Cell.Value, " "))))
    Cell.Value = LastName & ", " & FirstMidName
Next Cell

Handler:
Call FasterMacros(False) ' Optional but recommended

End Sub

Sub FasterMacros(ByVal Toggle As Boolean)
' To activate: Call FasterMacros(True)
' To stop: Call FasterMacros(False)
' Best practice is to ensure it is disabled after an error

Application.ScreenUpdating = Not Toggle
Application.EnableEvents = Not Toggle
Application.DisplayAlerts = Not Toggle
Application.Calculation = IIf(Toggle, xlCalculationManual, xlCalculationAutomatic)

End Sub

